*name Hamurabi
*call setftn:one
*no list
*no load list
*forex
c     ******************************************************************
c
c     Hamurabi
c
c     Converted from the original Focal program and modified for
c     EduSystem 70 by David H. Ahl, Digital. Modified for 8k Microsoft
c     Basic by Peter Turnbull. Converted to Fortran 77 by Philipp Engel.
c
c     ******************************************************************
      program hamura
      external play
c
c     Initialise pseudo-random number generator, output the header, and
c     start a new game.
c
      call rndm1(jextra(0, 40, 55))
c
      write (54, 100)
      call play()
c
  100 format (31x,' hamurabi',/,
     &26x,' creative computing',/,24x,' morristown, new jersey',/,/,
     &' try your hand at governing ancient sumeria',/,
     &' for a ten-year term of office.')
      end
c     ******************************************************************
      integer function feed(istor)
c
c     Asks player how many bushels to feed to the people.
c
      external nobush, resign
      integer  istor
c
   10 continue
      write (54, 100)
      read (54, 200) feed
      if (feed .lt. 0) call resign()
      if (feed .gt. istor) then
        call nobush(istor)
        goto 10
      end if
      istor = istor - feed
c
  100 format (' how many bushels do you wish to feed your people?')
  200 format (i6)
      end
c     ******************************************************************
      integer function plant(iacre, ipopu, istor)
c
c     Let the farming begin.
c
      external noacre, nobush, resign
      integer  iacre, ipopu, istor
c
   10 continue
      write (54, 100)
      read (54, 300) plant
c     if (istat .ne. 0) goto 10
      if (plant .eq. 0) return
      if (plant .lt. 0) call resign()
c
c     Trying to plant more acres than the player owns?
c
      if (plant .gt. iacre) then
        call noacre(iacre)
        goto 10
      end if
c
c     Enough grain for seed?
c
      if (int(plant / 2.0) .gt. istor) then
        call nobush(istor)
        goto 10
      end if
c
c     Enough people to tend the crops?
c
      if (plant .ge. 10 * ipopu) then
        write (54, 200) ipopu
        goto 10
      end if
c
      istor = istor - int(plant / 2.0)
c
  100 format (' how many acres do you wish to plant with seed?')
  200 format (' but you have only ',i3,' people to tend the field!',
     &' now then,')
  300 format (i6)
      end
c     ******************************************************************
      subroutine fink()
c
c     Player has been impeached.
c
      external quit
c
      write (54, 100)
      call quit()
  100 format (' due to this extreme mismanagement you have not only',
     &/,' been impeached and thrown out of office but you have',
     &/,' also been declared national fink!!')
      end
c     ******************************************************************
      subroutine noacre(iacre)
c
c     Not enough acres.
c
      integer iacre
c
      write (54, 100) iacre
  100 format (' hamurabi: think again. you own only ',i4,' acres.',
     &' now then,')
      end
c     ******************************************************************
      subroutine nobush(istor)
c
c     Not enough bushels of grain.
c
      integer istor
c
      write (54, 100) istor
  100 format (' hamurabi: think again. you have only ',i4,
     &' bushels of grain. now then,')
      end
c     ******************************************************************
      subroutine play()
c
c     The game starts here.
c
      external review, fink
      integer  feed, plant
c
      integer  ifeed, iplan, iquot, irand
      integer  ipopu, iharv, istor, iyiel, irats, iacre, iimmi, iplag,
     &         idead, iperc, ipric, istar, iyear
c
      common /global/ ipopu, iharv, istor, iyiel, irats, iacre, iimmi,
     &                iplag, idead, iperc, ipric, istar, iyear
c
c     initialise the game state.
c
      irats = iharv - istor
      iacre = iharv / iyiel
c
c     the main loop.
c
   10 continue
      iyear = iyear + 1
      ipopu = ipopu + iimmi
      write (54, 100) iyear, istar, iimmi
c
c     A plague strikes! half the population died.
c
      if (iplag .le. 0) then
        ipopu = ipopu / 2
        write (54, 200)
      end if
c
      write (54, 300) ipopu, iacre, iyiel, irats, istor
c
c     Max. number of rounds reached: review performance and quit.
c
      if (iyear .eq. 11) call review(iacre, ipopu, iperc, idead)
c
c     Roll new price per acre.
c
      ipric = int(10 * rndm(-1)) + 17
c
c     Ask the player to buy/sell land.
c
      write (54, 400) ipric
      call trade(iacre, istor, ipric)
      ifeed = feed(istor)
      iplan = plant(iacre, ipopu, istor)
c
c     A bountiful harvest!
c
      iyiel = 1 + int(rndm(-1) * 5)
      iharv = iplan * iyiel
      irats = 0
c
c     Rats are running wild.
c
      if (int(iyiel / 2.0) .eq. iyiel / 2) then
        irand = 1 + int(rndm(-1) * 5)
        irats = istor / irand
      end if
c
      istor = istor - irats + iharv
c
c     Let's have some babies. (Actually, it's immigration.)
c
      irand = 1 + int(rndm(-1) * 5)
      iimmi = int(irand * (20 * iacre + istor) / ipopu / 100 + 1)
c
c     Horrors, a 15% chance of plague.
c
      iplag = int(10 * (2 * rndm(-1) - 0.3))
      iquot = ifeed / 20
c
c     Either a new year, or impeachment if too many people starved.
c
      if (ipopu .lt. iquot) then
        istar = 0
      else
        istar = ipopu - iquot
        if (istar .gt. 0.45 * ipopu) then
          write (54, 500) istar
          call fink()
        end if
        iperc = ((iyear - 1) * iperc + istar * 100 / ipopu) / iyear
        ipopu = iquot
        idead = idead + istar
      end if
      goto 10
c
  100 format (/,' hamurabi: i beg to report to you,',/,/,
     &11x,'in year ',i2,', ',i2,' people starved, ',i2,
     &' came to the city.')
  200 format (/,11x,'a horrible plague struck! half the people died.',/)
  300 format (11x,'population is now ',i3,'.',/,
     &11x,'the city now owns ',i4,' acres.',/,
     &11x,'you harvested ',i4,' bushels per acre.',/,
     &11x,'the rats ate  ',i4,' bushels.',/,
     &11x,'you now have  ',i4,' bushels in store.',/)
  400 format (' land is trading at ',i2,' bushels per acre.')
  500 format (/,' you starved ',i2,' people in one year!!')
      end
c     ******************************************************************
      subroutine resign()
c
c     The steward has enough.
c
      external quit
c
      write (54, 100)
      call quit()
  100 format (' hamurabi: i cannot do what you wish.',
     &' get yourself another steward!!')
      end
c     ******************************************************************
      subroutine review(iacre, ipopu, iperc, idead)
c
c     Outputs results.
c
      external fink, quit
      real     rand
c
      integer  iacre, ipopu, iperc, idead
      integer  iland
c
      iland = iacre / ipopu
      write (54, 100) iperc, idead, iland
c
      if (iperc .gt. 33 .or. iland .lt. 7) call fink()
c
      if (iperc .gt. 10 .or. iland .lt. 9) then
        write (54, 200)
      else if (iperc .gt. 3 .or. iland .lt. 10) then
        write (54, 300) int(ipopu * 0.8 * rndm(-1))
      else
        write (54, 400)
      end if
c
      call quit()
c
  100 format (' in your 10-year term of office, ',i2,
     &' percent of the',/,' population starved per year on the',
     &' average, i.e. a total of',/,1x,i2,' people died!!',/,
     &' you started with 10 acres per person and ended with',/,
     &1x,i2,' acres per person.')
  200 format (' your heavy-handed performance smacks of nero and',
     &' ivan iv.',/,' the people (remaining) find you an unpleasant',
     &' ruler, and,',/,' frankly, hate your guts!!')
  300 format (' your performance could have been somewhat better,',
     &' but',/,' really wasn'1h''t too bad at all. ',i2,' people',/,
     &' would dearly like to see you assassinated but we all have our',
     &/,' trivial problems.')
  400 format (' a fantastic performance!! charlemange, disraeli, and',
     &/,' jefferson combined could not have done better!')
      end
c     ******************************************************************
      subroutine trade(iacre, istor, ipric)
c
c     Lets the player buy/sell acres.
c
      external noacre, nobush, resign
      integer  iacre, istor, ipric
      integer  iamou
c
c     Ask to buy acres.
c
   10 continue
      write (54, 100)
      read (54, 300) iamou
c
      if (iamou .lt. 0) call resign()
c
      if (ipric * iamou .gt. istor) then
        call nobush(istor)
        goto 10
      end if
c
      if (iamou .gt. 0) then
        iacre = iacre + iamou
        istor = istor - ipric * iamou
        return
      end if
c
c     If no acres bought, ask how many to sell.
c
   20 continue
      write (54, 200)
      read (54, 300) iamou
c
      if (iamou .lt. 0) call resign()
c
      if (iamou .ge. iacre) then
        call noacre(iacre)
        goto 20
      end if
c
      if (iamou .gt. 0) then
        iacre = iacre - iamou
        istor = istor + ipric * iamou
        return
      end if
c
  100 format (' how many acres do you wish to buy?')
  200 format (' how many acres do you wish to sell?')
  300 format (i6)
      end
c     ******************************************************************
      subroutine quit()
c
c     Prints last message and exits.
c
      write (54, 100)
      stop
  100 format (/,' so long for now.')
      end
c     ******************************************************************
      block data
c
c     Common variables:
c
c     ipopu - size of the population.
c     iharv - bushels harvested in one year.
c     istor - number of bushels stored.
c     iyiel - yield of acres.
c     irats - bushels eaten by rats.
c     iacre - acres owned by player.
c     iimmi - immigration per year.
c     iplag - flag for horrible plague.
c     idead - number of people who died.
c     iperc - percentage of people who died.
c     ipric - price per acre.
c     istar - number of people starved to death in one year.
c     iyear - the current year.
c
      common /global/
     &ipopu, iharv, istor, iyiel, irats, iacre, iimmi, iplag, idead,
     &iperc, ipric, istar, iyear
      data
     &ipopu, iharv, istor, iyiel, irats, iacre, iimmi, iplag, idead,
     &iperc, ipric, istar, iyear /95,3000,2800,3,0,0,5,1,0,0,0,0,0/
      end
*library:1,23
*execute
*end file
